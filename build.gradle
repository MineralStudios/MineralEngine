buildscript {
    ext.kotlin_version = '2.1.20'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'maven-publish'
    apply plugin: 'pmd'

    repositories {
        mavenCentral()
        maven {
            url = "https://repo.papermc.io/repository/maven-public/"
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    compileJava {
        options.compilerArgs += ['-parameters']
    }

    pmd {
        rulesMinimumPriority = 4
    }

    kotlin {
        jvmToolchain(21)
    }

    group = "gg.mineral.engine"
    version = new Date().format('yyyyMMdd')

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = project.name
                version = new Date().format("yyyyMMdd-HH.mm.ss")
            }
        }

        repositories {
            maven {
                name = "mineralDevPrivate"
                url = uri("https://repo.mineral.gg/private")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}
